# ─────────────────────────────────────────────────────────────
# Insertion Sort - Máquina RAM
# Registros:
#   R1  = array A (indexado desde 1)
#   R2  = n
#   R3  = j (bucle exterior) / contador lectura-escritura
#   R4  = i (bucle interior)
#   R5  = key
#   R6  = auxiliar i+1
#   R7  = n+1 (límite bucles)
# Entrada: n A[1] A[2] ... A[n]
# ─────────────────────────────────────────────────────────────

READ 2
LOAD 2
ADD =1
STORE 7

LOAD =1
STORE 3

LEER_ARRAY:	LOAD 7
		SUB 3
		JGTZ HACER_LECTURA
		JUMP FIN_LECTURA

HACER_LECTURA:	READ 1[*3]
		LOAD 3
		ADD =1
		STORE 3
		JUMP LEER_ARRAY

FIN_LECTURA:	LOAD =2
		STORE 3

FOR_J:		LOAD 7
		SUB 3
		JGTZ CUERPO_FOR_J
		JUMP FIN_SORT

CUERPO_FOR_J:	LOAD 1[*3]
		STORE 5
		LOAD 3
		SUB =1
		STORE 4

WHILE:		LOAD 4
		JGTZ COND2
		JUMP FIN_WHILE

COND2:		LOAD 1[*4]
		SUB 5
		JGTZ CUERPO_WHILE
		JUMP FIN_WHILE

CUERPO_WHILE:	LOAD 4
		ADD =1
		STORE 6
		LOAD 1[*4]
		STORE 1[*6]
		LOAD 4
		SUB =1
		STORE 4
		JUMP WHILE

FIN_WHILE:	LOAD 4
		ADD =1
		STORE 6
		LOAD 5
		STORE 1[*6]
		LOAD 3
		ADD =1
		STORE 3
		JUMP FOR_J

FIN_SORT:	LOAD =1
		STORE 3

ESCRIBIR:	LOAD 7
		SUB 3
		JGTZ HACER_ESCRITURA
		JUMP FIN

HACER_ESCRITURA: WRITE 1[*3]
		LOAD 3
		ADD =1
		STORE 3
		JUMP ESCRIBIR

FIN:		HALT
