# BubbleSort
# for i = 1 to n-1 do
# 	for j = n downto i + 1 do
# 		if A[j-1] > A[j] then begin
# 			temp = A[j-1]
# 			A[j-1] = A[j]
# 			A[j] = temp
# 		end
# 	end
# end

# TABLA DE REGISTROS
#   R1  = array A (indexado desde 1)
#   R2  = n
#   R3  = i (bucle exterior)
#   R4  = j (bucle interior)
#   R5  = temp (swap)
#   R6  = j-1
#   R7  = n+1 (l√≠mite bucles)
# Entrada: n A[1] A[2] ... A[n]


READ 2
LOAD 2
ADD =1
STORE 7

LOAD =1
STORE 3

LEER_ARRAY:	LOAD 7
		SUB 3
		JGTZ HACER_LECTURA
		JUMP FIN_LECTURA

HACER_LECTURA:	READ 1[*3]
		LOAD 3
		ADD =1
		STORE 3
		JUMP LEER_ARRAY

FIN_LECTURA:	LOAD =1
		STORE 3

FOR_I:		LOAD 2
		SUB 3
		JGTZ INIT_J
		JUMP FIN_SORT

INIT_J:		LOAD 2
		STORE 4

FOR_J:		LOAD 4
		SUB 3
		JGTZ COMPARAR
		JUMP SIGUIENTE_I

COMPARAR:	LOAD 4
		SUB =1
		STORE 6
		LOAD 1[*6]
		SUB 1[*4]
		JGTZ SWAP
		JUMP SIGUIENTE_J

SWAP:		LOAD 1[*6]
		STORE 5
		LOAD 1[*4]
		STORE 1[*6]
		LOAD 5
		STORE 1[*4]

SIGUIENTE_J:	LOAD 4
		SUB =1
		STORE 4
		JUMP FOR_J

SIGUIENTE_I:	LOAD 3
		ADD =1
		STORE 3
		JUMP FOR_I

FIN_SORT:	LOAD =1
		STORE 3

ESCRIBIR:	LOAD 7
		SUB 3
		JGTZ HACER_ESCRITURA
		JUMP FIN

HACER_ESCRITURA: WRITE 1[*3]
		LOAD 3
		ADD =1
		STORE 3
		JUMP ESCRIBIR

FIN:		HALT